apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    Properties versionProps = loadProperties('version.properties')
    def versionCodeProperty = versionProps['VERSION_CODE'].toInteger()
    defaultConfig {
        applicationId "com.vaxapp.thingspeakviewer"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionCodeProperty
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug-key/debug.keystore')
            storePassword 'android'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFile 'proguard-files/proguard-rules.pro'
            proguardFile 'proguard-files/proguard-gson.pro'
            proguardFile 'proguard-files/proguard-rxjava.pro'
            proguardFile 'proguard-files/proguard-square-retrofit.pro'
            proguardFile 'proguard-files/proguard-support-v7-appcompat.pro'
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

private Properties loadProperties(String fileName) {
    Properties versionProps = new Properties()
    def versionPropsFile = file(fileName)
    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
    } else {
        throw new GradleException("Could not read ".fileName)
    }
    versionProps
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    def appDependencies = rootProject.ext.appDependencies
    def commonDependencies = rootProject.ext.commonDependencies
    def testDependencies = rootProject.ext.commonTestDependencies
    def androidTestDependencies = rootProject.ext.commonAndroidTestDependencies
    implementation commonDependencies.kotlin
    implementation commonDependencies.appCompat
    implementation commonDependencies.rxAndroid
    implementation commonDependencies.dagger
    annotationProcessor commonDependencies.daggerCompiler
    implementation appDependencies.vectorDrawable
    implementation appDependencies.recyclerView
    implementation appDependencies.design
    implementation appDependencies.constraintLayout
    implementation appDependencies.anko
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true;
    }
    testImplementation testDependencies.jUnit
    androidTestImplementation androidTestDependencies.supportTestRunner
    androidTestImplementation androidTestDependencies.espressoCore
    implementation project(':domain')
    implementation project(':data')
}
